import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id 'org.jetbrains.intellij' version '0.4.10'
    id 'de.undercouch.download' version '4.0.0'
    id "com.palantir.git-version" version "0.12.0-rc2"
}

group 'com.instana.idea.php'

def jenkinsBuildNumber = buildNumber.toInteger()
def details = versionDetails()

if (details.isCleanTag) {
    version = "${details.lastTag}"
}else if(jenkinsBuildNumber > 0) {
    version = "${details.lastTag}.${jenkinsBuildNumber}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

intellij {
    version ideaVersion
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            'java',
            'java-i18n',
            'CSS',
            'properties',
    ]
}

buildSearchableOptions.enabled = false

patchPluginXml {
    sinceBuild customSinceBuild
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

publishPlugin {
    token System.getenv('IJ_REPO_TOKEN')
    channels publishChannel
}

task downloadInstanaStubs(type: Download) {
    src "https://raw.githubusercontent.com/instana/instana-php-sdk/${instanaPhpSdkVersion}/instana.php"
    dest "src/main/resources/instana/instana.php"
}

compileJava.dependsOn downloadInstanaStubs
